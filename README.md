# Recurrent

Recurrent is an experiment in writing Reactive GUIs using FRP all the way down.

## Installation

Add `[recurrent "0.1.1-SNAPSHOT"]` to your project.clj dependencies.

## About

Components in Recurrent can be thought of as pure functions taking two maps.  One, called `props`contains traditional (read static) data.  It's analogous to the data that might get passed into a React component.  The other is special. It contains time varying data built with [Ulmus](https://github.com/jeremykross/ulmus), calls signals.  By using signals, components can behave statefully while being conceptually functional.  Components (as functions) return a map of signals called `sinks` that can be fed as `sources` to other signals.

One source and sink respectively, in a standard Recurrent component, is treated specially.  It can be found at the value of the first key in the sources list (see below), and must be the first sink returned. As a source, this value of this key is a function of two arguments: a query selector, and an event (i.e. `((:dom-$ sources) ".button" "click")`)  It returns a signal of those event occurring over time on the component.

As a sink, this key must return a series of hiccup-style DOM descriptions (i.e. `[:button "Click Me!"]`).  These will get diffed into the actual DOM successively, as the signal generates values.

## Example
I know that's a lot to take in. Hopefully the following example will clarify things.  In the example below we create two components.  `TextInput` creates an `<input>` of type text.  It takes an `inital-value` on `props` and returns it's current value (`value-$`) as a sink.  Notice how the DOM description is generated by mapping over this sink.

The value of the text input is used to set the value of our second component, a simple label.

```clojure
(ns recurrent-hello-john.core
  (:require
    garden.core
    recurrent.drivers.dom
    [dommy.core :include-macros true]
    [recurrent.core :as recurrent :include-macros true]
    [ulmus.core :as ulmus]))

(recurrent/defcomponent TextInput

  ; keys expected in props and sources
  [:initial-value] [:dom-$]

  ; constructor, return value used as the value of this
  #()

  ; some css, garden style
  [:.text-input {:margin "16px"}]

  ; Our first return signal (called a sink)
  ; creates a signal of successive virutal-dom (hiccup style) to get diffed into real dom.
  :dom-$ (fn [this props sources sinks]
           (ulmus/map (fn [value]
                        [:input {:class "text-input"
                                 :type "text"
                                 :value value}])
                      (:value-$ sinks)))

  ; Another sink
  ; Keeps track of the current value
  :value-$ (fn [this props sources sinks]
             (ulmus/start-with! (:initial-value props)
               (ulmus/map
                 (fn [evt] (.-value (.-target evt)))
                 ; a special souce, available under the first key in the source list.
                 ; takes a query-selector and an event
                 ; returns a signal of occurances of the event.
                 ((:dom-$ sources) "input" "keydown")))))

(recurrent/defcomponent Label
  [] [:dom-$ :name-$]

  ; Constructor
  #()

  ; Style
  [:label {:margin-left "42px"}]

  :dom-$ (fn [_ _ sources _]
           (ulmus/map
             (fn [n]
               [:label (str "Hello, " n ".")])
             (:name-$ sources))))


(defn Main
  [props sources]
  (let [input (TextInput {:initial-value "John"} {:dom-$ (:dom-$ sources)})
        label (Label {} {:dom-$ (:dom-$ sources)
                         :name-$ (:value-$ input)})]
    {:dom-$
     (ulmus/map
       (fn [[input-dom label-dom]]
         [:div input-dom label-dom])
       (ulmus/latest (:dom-$ input) (:dom-$ label)))}))

(defn main!
  []
  (recurrent/run!
    Main
    {:dom-$ (recurrent.drivers.dom/from-id "app")}))
```

You can find the [full example here](https://github.com/jeremykross/recurrent-hello-john).

You might also like [recurrent-todo](https://github.com/jeremykross/recurrent-todo).

## Prior Art
Many, many, thanks to the great [cycle.js](https://github.com/cyclejs/cyclejs) which inspired this work.

## Thanks
Recurrent uses [garden](https://github.com/noprompt/garden) for CSS generation and [dommy](https://github.com/plumatic/dommy) for dom manipulation.

## License

Copyright 2018 Jeremy Kross

Distributed under the Eclipse Public License either version 1.0 or (at your option) any later version.
